:name dropchance_calculator

key.b()

; Do not change this impulse! This allows for time reset.
game.newround()

#d dropchance

#difficultyfactor (1.0 + (0.25 * (floor(i2d(index("{difficultystring}",difficulty,0))/10.0))))

#difficultystring \
easy------\
----------\
mediumhard\
----------\
insane----\
nightmare-\
impossible

#varstr \
thiswave--\
peak------\
persecond-\

#varstr2 \
dropchancethiswave--\
dropchancepeak------\
dropchancepersecond-\
savedtimez----------\

#wavetotal ((wave()/10.0)+(era()*1e11)+(infinity()*1e22))

; switches the condition (impulse !== newround) if toggle is off
impulse() != \
   if({d}toggle == 0,\
      "game.newround",\
   "lol")

:global double {d}peak
:global double {d}thiswave
:local double {d}factor
:local double basemodule{d}
:local string difficulty
:global int {d}toggle
:global double {d}persecond
:local int i

aaa:
{d}toggle = \
   if(impulse() != "game.newround",\
      1 - {d}toggle,\
   {d}toggle)

timereset:
; sets savedtimez to timestamp if impulse == newround or (is towertesting & savedtimez is not yet set).
gds("savedtimez",\
   if(impulse() == "game.newround"|\
   (isTowerTesting()&\
   gdg("savedtimez") == 0.),\
      now(),\
   gdg("savedtimez"))\
)

wait:
; stops the current instance if impulse == newround. if toggle is on, then if is towertesting, goto starttt, else goto wait, else, goto unset
goto(\
   if(impulse() == "game.newround",\
      69,\
   if({d}toggle == 1,\
      if(isTowerTesting(),\
         starttt,\
      wait),\
   unset))\
)

starttt:
; initialize factors
lds("{d}factor", 1.0)
basemodule{d} = 0.01
difficulty = "easy"

mainloop:
; sets the vars in varstr with their respective expressions according to the index.
gds(\
   if(i != 0,\
      "{d}" . sub("{varstr}", (i - 1) * ({len({varstr})}/3), index("{varstr}", "-", (i - 1) * ({len({varstr})}/3)) - ((i - 1) * ({len({varstr})}/3))),\
   ""),\
   if(i == 1,\
      if(wave() > 10.|\
      (era() > 0.&\
      infinity() > 0.),\
         {difficultyfactor} * basemodule{d} * {d}factor * ({wavetotal}//10.0),\
      0.),\
   if(i == 2,\
      max({d}thiswave, {d}peak),\
   if(i == 3,\
      {d}thiswave/((now() - gdg("savedtimez"))/1e7),\
   0.)))\
)

unset:
i = (i % 4) + 1

; unsets all vars in the varstr2 macro
gu(\
   if({d}toggle == 0,\
      sub("{varstr2}", (i - 1) * 20, index("{varstr2}", "-", (i - 1) * 20) - (i - 1) * 20 ),\
   "")\
)

; goes to unset if toggle is off and if vars != 0. goes to wait if not towertesting. resets time if savedtimez isn't set.
goto(\
   if({d}toggle == 1,\
      if(isTowerTesting(),\
         if(gdg("savedtimez") != 0.,\
            mainloop,\
         timereset),\
      wait),\
   if({d}thiswave == 0.&\
   {d}persecond == 0.&\
   {d}peak == 0.&\
   gdg("savedtimez") == 0.,\
      69,\
   unset))\
)
